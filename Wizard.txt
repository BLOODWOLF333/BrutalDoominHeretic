ACTOR HEWizard : Wizard Replaces Wizard
{
	+NOBLOOD
	States
	{
	Spawn:
		WZRD AB 10 A_Look
		Loop
	See:
		TNT1 A 0 A_Stop
		WZRD A 3 A_Chase
		WZRD A 4 A_Chase
		WZRD A 3 A_Chase
		WZRD A 4 A_Chase
		WZRD B 3 A_Chase
		WZRD B 4 A_Chase
		WZRD B 3 A_Chase
		WZRD B 4 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_Stop
		WZRD C 4 A_WizAtk1
		WZRD C 4 A_WizAtk2
		WZRD C 4 A_WizAtk1
		WZRD C 4 A_WizAtk2
		WZRD C 4 A_WizAtk1
		WZRD C 4 A_WizAtk2
		WZRD C 4 A_WizAtk1
		WZRD C 4 A_WizAtk2
		WZRD D 12 A_WizAtk3
		Goto See
	Pain:
		WZRD E 3 A_GhostOff
		WZRD E 3 A_Pain
		Goto See
	Death:
		WZRD F 6 A_GhostOff
		WZRD G 6 A_Scream
		WZRD HI 6
		WZRD J 6 A_NoBlocking
		WZRD KL 6
		WZRD M -1 A_SetFloorClip
		Stop
	}
}

ACTOR HEWizardFX1 Replaces WizardFX1
{
	Game Heretic
	SpawnID 140
	Radius 10
	Height 6
	Speed 18
	FastSpeed 24
	Damage 3
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	RenderStyle Add
	States
	{
	Spawn:
		FX11 AB 6 BRIGHT
		Loop
	Death:
		FX11 C 5 BRIGHT
		TNT1 A 0 A_CustomMissile ("VioletFire", 0, 0, random (0, 360), CMF_AIMDIRECTION | CMF_ABSOLUTEPITCH | CMF_ABSOLUTEANGLE, random(60,90))
		TNT1 A 0 A_SpawnItemEx("VioletParticleSpawner")
		TNT1 A 0 A_CustomMissile ("ExplosionSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		FX11 DEFG 5 BRIGHT
		Stop
	}
}
ACTOR HECrossbow : Crossbow
{
	Weapon.SelectionOrder 800
	Weapon.AmmoUse 1
	Weapon.AmmoGive 10
	Weapon.AmmoType "HECrossbowAmmo"
	Weapon.SisterWeapon "HECrossbowPowered"
	Weapon.YAdjust 15
	Inventory.PickupMessage "$TXT_WPNCROSSBOW"
	DropItem "Crossbow"
	VisibleToPlayerClass Corvus
	Inventory.ForbiddenTo BDoomer, ArcadeDoomer, TacticalDoomer, Purist
	States
	{
	Spawn:
		WBOW A -1
		Stop
	Ready:
		CRBW AAAAAABBBBBBCCCCCC 1 A_WeaponReady
		Loop
	Deselect:
		CRBW A 1 A_Lower
		Loop
	Select:
		CRBW A 1 A_Raise
		Loop
	Fire:
		CRBW D 6 A_FireCrossbowPL1
		CRBW EFGH 3
		CRBW AB 4
		CRBW C 5 A_ReFire
		Goto Ready
	}
}

ACTOR HECrossbowPowered : HECrossbow Replaces CrossbowPowered
{
	+WEAPON.POWERED_UP
	Weapon.AmmoGive 0
	Weapon.SisterWeapon "HECrossbow"
	DropItem "HECrossbow"
	States
	{
	Fire:
		TNT1 A 0 A_PlaySound("weapons/bowshoot", CHAN_WEAPON)
		CRBW D 5 A_FireCustomMissile("HECrossbowFX2",0,1)
		CRBW E 3
		CRBW F 2
		CRBW G 3
		CRBW H 2
		CRBW A 3
		CRBW B 3
		CRBW C 4 A_ReFire
		Goto Ready
	}
}

ACTOR HECrossbowFX1 Replaces CrossbowFX1
{
	Game Heretic
	SpawnID 147
	Radius 11
	Height 8
	Speed 45
	Damage (random(60,60))
	Projectile
	RenderStyle Add
	SeeSound "weapons/bowshoot"
	DeathSound "weapons/bowhit"
	Obituary "$OB_MPCROSSBOW"
	+BLOODLESSIMPACT
	States
	{
	Spawn:
		FX03 B 1 BRIGHT
		Loop
	Death:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("GreenParticleSpawner")
		FX03 HIJ 8 BRIGHT
		Stop
	}
}

ACTOR HECrossbowFX2 : FastProjectile Replaces CrossbowFX2
{
	Game Heretic
	SpawnID 148
	Radius 11
	Height 8
	Speed 140
	Damage (random(108,108))
	Projectile
	RenderStyle Add
	SeeSound "weapons/bowshoot"
	DeathSound "weapons/bowhit"
	Obituary "$OB_MPPCROSSBOW"
	+BLOODLESSIMPACT
	States
	{
	Spawn:
		FX03 B 1 BRIGHT A_SpawnItemEx("CrossbowFX4", random2[BoltSpark]()*0.015625, random2[BoltSpark]()*0.015625, 0, 0,0,0,0,SXF_ABSOLUTEPOSITION, 50)
		Loop
	Death:
		FX03 H 8 BRIGHT A_Explode
		TNT1 A 0 A_CustomMissile("ExplosionSplashSpawner")
		TNT1 A 0 A_SpawnItemEx("ExplosiveFlamesG",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_CustomMissile("HEExplosionparticles3G", 0, 0, random (0, 360), 2, random (40, 90))
		TNT1 A 0 A_CustomMissile("HEExplosionparticles4G", 0, 0, random (0, 360), 2, random (40, 90))
		TNT1 A 0 A_CustomMissile("BigBlackSmokeLarger", 0, 0, random (0, 360), 2, random (20, 90))
		TNT1 A 0 Radius_Quake(3, 8, 0, 15, 0)
		FX03 IJ 8 BRIGHT
		Stop
	}
}

ACTOR HECrossbowFX3 : HECrossbowFX1 Replaces CrossbowFX3
{
	Game Heretic
	SpawnID 149
	Speed 45
	Damage (random(12,12))
	SeeSound ""
	-NOBLOCKMAP
	+WINDTHRUST
	+THRUGHOST
	+BLOODLESSIMPACT
	States
	{
	Spawn:
		FX03 A 1 BRIGHT
		Loop
	Death:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("GreenParticleSpawner")
		FX03 CDE 8 BRIGHT
		Stop
	}
}

ACTOR HECrossbowFX4 Replaces CrossbowFX4
{
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+SHADOW
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	+DONTSPLASH
	RenderStyle Add
	States
	{
	Spawn:
		FX03 JE 8 BRIGHT
		Stop
	}
}
ACTOR HEMummy Replaces Mummy
{
	Game Heretic
	SpawnID 4
	Health 80
	Radius 22
	Height 62
	Mass 75
	Speed 12
	Painchance 128
	Monster
	+FLOORCLIP
	+NOBLOOD
	SeeSound "mummy/sight"
	AttackSound "mummy/attack1"
	PainSound "mummy/pain"
	DeathSound "mummy/death"
	ActiveSound "mummy/active"
	HitObituary "$OB_MUMMY"
	DropItem "Clip1Drop", 84, 3
	Bloodtype "MummyBlood"
	States
	{
	Spawn:
		MUMM AB 10 A_Look
		Loop
	See:
		MUMM ABCD 4 A_Chase
		Loop
	Melee:
		MUMM E 6 A_FaceTarget
		MUMM F 6 A_CustomMeleeAttack(random[MummyAttack](1,8)*2, "mummy/attack2", "mummy/attack")
		MUMM G 6
		Goto See
	Pain:
		MUMM H 4
		MUMM H 4 A_Pain
		Goto See
	Death:
		MUMM I 5
		MUMM J 5 A_Scream
		MUMM K 5 A_SpawnItemEx("MummySoul", 0,0,10, 0,0,1)
		MUMM L 5
		MUMM M 5 A_NoBlocking
		MUMM NO 5
		MUMM P -1
		Stop
	}
}

ACTOR HEMummyLeader : HEMummy Replaces MummyLeader
{
	Game Heretic
	SpawnID 2
	Species "MummyLeader"
	Health 100
	Painchance 64
	Obituary "$OB_MUMMYLEADER"
	States
	{
	Missile:
		MUMM X 5 A_FaceTarget
		MUMM Y 5 Bright A_FaceTarget
		MUMM X 5 A_FaceTarget
		MUMM Y 5 Bright A_FaceTarget
		MUMM X 5 A_FaceTarget
		MUMM Y 5 Bright A_CustomComboAttack("MummyFX1", 32, random[MummyAttack2](1,8)*2, "mummy/attack2")
		Goto See
	}
}

ACTOR HEMummyGhost : HEMummy Replaces MummyGhost
{
	Game Heretic
	SpawnID 8
	+SHADOW
	+GHOST
	RenderStyle Translucent
	Alpha 0.4
}

ACTOR HEMummyLeaderGhost : HEMummyLeader Replaces MummyLeaderGhost
{
	Game Heretic
	SpawnID 9
	Species "MummyLeaderGhost"
	+SHADOW
	+GHOST
	RenderStyle Translucent
	Alpha 0.4
}

ACTOR HEMummyFX1 Replaces MummyFX1
{
	Game Heretic
	SpawnID 131
	Radius 8
	Height 14
	Speed 9
	FastSpeed 18
	Damage 4
	RenderStyle Add
	Projectile
	-ACTIVATEPCROSS
	-ACTIVATEIMPACT
	+SEEKERMISSILE
	Scale 0.5
	States
	{
	Spawn:
		FX15 A 1 Bright
		FX15 A 4 Bright A_PlaySound("mummy/head")
		TNT1 A 0 A_CustomMissile ("SkullTrails", 0, 0, random (0, 360), 2, random (0, 160))
		FX15 B 4 Bright A_SeekerMissile(10,20)
		TNT1 A 0 A_CustomMissile ("SkullTrails", 0, 0, random (0, 360), 2, random (0, 160))
		FX15 C 4 Bright
		TNT1 A 0 A_CustomMissile ("SkullTrails", 0, 0, random (0, 360), 2, random (0, 160))
		FX15 B 4 Bright A_SeekerMissile(10,20)
		TNT1 A 0 A_CustomMissile ("SkullTrails", 0, 0, random (0, 360), 2, random (0, 160))
		FX15 A 4 Bright
		TNT1 A 0 A_CustomMissile ("SkullTrails", 0, 0, random (0, 360), 2, random (0, 160))
		FX15 B 4 Bright A_SeekerMissile(10,20)
		TNT1 A 0 A_CustomMissile ("SkullTrails", 0, 0, random (0, 360), 2, random (0, 160))
		FX15 C 4 Bright
		TNT1 A 0 A_CustomMissile ("SkullTrails", 0, 0, random (0, 360), 2, random (0, 160))
		FX15 B 4 Bright A_SeekerMissile(10,20)
		TNT1 A 0 A_CustomMissile ("SkullTrails", 0, 0, random (0, 360), 2, random (0, 160))
		Goto Spawn+1
	Death:
		FX15 D 5 BRIGHT
		TNT1 A 0 A_CustomMissile ("HEExplosionparticlesSmall", 10, 0, random (0, 180), 2, random (40, 90))
	    TNT1 A 0 A_CustomMissile ("FireBallExplosionFlames", 1, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_SpawnItemEx ("TinyBurningPiece3", random (-15, 15), random (-15, 15))
		TNT1 A 0 A_CustomMissile ("ExplosionSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		FX15 EFG 5 Bright
		Stop
	}
}

ACTOR SkullTrails
{
	Game Doom
	Radius 1
	Height 1
	Speed 2
	Damage 0
    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+MISSILE
	+FORCEXYBILLBOARD
    +CLIENTSIDEONLY
	RenderStyle Add
    Scale 0.5
	Alpha 0.6
	Gravity 0
	States
	{
    Spawn:
        FYFX K 6 BRIGHT
		FYFX L 5 BRIGHT
		FYFX M 4 BRIGHT
		FYFX N 3 BRIGHT
		FYFX O 2 BRIGHT
		FYFX P 1 BRIGHT
        Stop
	}
}
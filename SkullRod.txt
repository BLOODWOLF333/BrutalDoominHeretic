ACTOR HESkullRod : SkullRod Replaces SkullRod
{
	Game Heretic
	SpawnID 30
	Weapon.SelectionOrder 200
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 50
	Weapon.YAdjust 15
	Weapon.AmmoType1 "HESkullRodAmmo"
	Weapon.SisterWeapon "HESkullRodPowered"
	Inventory.PickupMessage "$TXT_WPNSKULLROD"
	DropItem "HESkullRod"
	VisibleToPlayerClass Corvus
	Inventory.ForbiddenTo BDoomer, ArcadeDoomer, TacticalDoomer, Purist
	States
	{
	Spawn:
		WSKL A -1
		Stop
	Ready:
		HROD A 1 A_WeaponReady
		Loop
	Deselect:
		HROD A 1 A_Lower
		Loop
	Select:
		HROD A 1 A_Raise
		Loop
	Fire:
		HROD A 4 A_FireSkullRodPL1
		HROD B 4 BRIGHT A_FireSkullRodPL1
		HROD B 0 A_ReFire
		Goto Ready
	}
}

ACTOR HESkullRodPowered : SkullRodPowered Replaces SkullRodPowered
{
	Game Heretic
	+WEAPON.POWERED_UP
	Weapon.AmmoUse1 30
	Weapon.AmmoGive1 0
	Weapon.AmmoType1 "HESkullRodAmmo"
	Weapon.SisterWeapon "HESkullRod"
	DropItem "HESkullRod"
	States
	{
	Fire:
		HROD C 2 BRIGHT
		HROD D 3 BRIGHT
		HROD E 2 BRIGHT
		HROD F 3 BRIGHT
		HROD G 4 BRIGHT A_FireSkullRodPL2
		HROD F 2 BRIGHT A_Recoil(4)
		HROD E 3 BRIGHT
		HROD D 2 BRIGHT
		HROD C 2 BRIGHT A_ReFire
		Goto Ready
	}
}

ACTOR HEHornRodFX1 : HornRodFX1 Replaces HornRodFX1
{
	Game Heretic
	SpawnID 160
	Radius 12
	Height 8
	Speed 44
	Damage (random(18,18))
	Projectile
	+WINDTHRUST
	-NOBLOCKMAP
	+BLOODLESSIMPACT
	RenderStyle Add
	Alpha 0.75
	SeeSound "weapons/hornrodshoot"
	DeathSound "weapons/hornrodhit"
	Obituary "$OB_MPSKULLROD"
	States
	{
	Spawn:
		FX00 AB 6 BRIGHT
		Loop
	Death:
		FX00 H 5 BRIGHT
		TNT1 A 0 A_SpawnItemEx("RedParticleSpawner")
		FX00 I 5 BRIGHT
		FX00 JK 4 BRIGHT
		FX00 LM 3 BRIGHT
		Stop
	}
}

ACTOR HEHornRodFX2 : HornRodFX2 Replaces HornRodFX2
{
	Radius 12
	Height 8
	Speed 22
	Damage (random(180,180))
	Projectile
	RenderStyle Add
	Alpha 0.75
	SeeSound "weapons/hornrodpowshoot"
	DeathSound "weapons/hornrodpowhit"
	Obituary "$OB_MPPSKULLROD"
	+BLOODLESSIMPACT
	States
	{
	Spawn:
		FX00 CDEF 3 BRIGHT
		Loop
	Death:
		FX00 H 5 BRIGHT A_BFGSpray("HornRodExtra",30)
		TNT1 A 0 A_CustomMissile("ExplosionSplashSpawner")
		TNT1 A 0 Radius_Quake (3, 8, 0, 15, 0)
		TNT1 AAAA 0 A_SpawnItemEx("RedParticleSpawner")
		FX00 I 5 BRIGHT
		FX00 J 4 BRIGHT
		FX00 KLM 3 BRIGHT
		Stop
	}
}

ACTOR HornRodExtra
{
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.75
	States
	{
	Spawn:
		FX00 H 5 BRIGHT
		TNT1 A 0 A_SpawnItemEx("RedParticleSpawner")
		FX00 I 5 BRIGHT
		FX00 J 4 BRIGHT
		FX00 KLM 3 BRIGHT
		Stop
	}
}
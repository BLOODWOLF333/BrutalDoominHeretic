Actor RavenStaff : Mace
{
	Game Heretic
	SpawnID 31
	Weapon.SelectionOrder 1400
	Weapon.AmmoUse 5
	Weapon.AmmoGive1 50
	Weapon.YAdjust 15
	Weapon.AmmoType "RavenAmmo"
	Weapon.SisterWeapon "RavenStaffPowered"
	Inventory.PickupMessage "Raven Staff"
	Tag "Raven Staff"
	DropItem "RavenStaff"
	VisibleToPlayerClass Corvus
	Inventory.ForbiddenTo BDoomer, ArcadeDoomer, TacticalDoomer, Purist
	States
	{
	Spawn:
		RVNS A -1
		Loop

	Select:
		RVNS B 1 A_Raise
		loop

	Deselect:
		RVNS B 1 A_Lower
		loop

	Ready:
		RVNS B 1 A_WeaponReady
		loop

	Fire:
		RVNS B 2
		RVNS B 0 A_PlayWeaponSound("Weapons/RavenStaff/Fire11")
		RVNS CD 3 BRIGHT
		RVNS B 0 A_FireCustomMissile("RavenMissile", 0, 1)
		RVNS B 0 A_FireCustomMissile("RavenMissile", 3, 0)
		RVNS B 0 A_FireCustomMissile("RavenMissile", -3, 0)
		RVNS B 0 A_FireCustomMissile("RavenMissile", 6, 0)
		RVNS B 0 A_FireCustomMissile("RavenMissile", -6, 0)
		RVNS E 6 BRIGHT
		RVNS DC 3 BRIGHT
		RVNS B 6
		Goto Ready
	}
}

Actor RavenStaffPowered : RavenStaff
{
	Game Heretic
	+WEAPON.POWERED_UP
	Weapon.AmmoUse 25
	Weapon.AmmoGive 0
	Weapon.SisterWeapon "RavenStaff"
	Tag "Raven Staff Powered"
	DropItem "RavenStaff"
	States
	{
	Fire:
		RVNS B 2
		RVNS B 0 A_PlayWeaponSound("Weapons/RavenStaff/Fire2")
		RVNS CD 3 BRIGHT
		RVNS B 0 A_FireCustomMissile("PoweredRavenMissile", 0, 1, 0)
		RVNS B 0 A_FireCustomMissile("PoweredRavenMissile", 3, 0, -3)
		RVNS B 0 A_FireCustomMissile("PoweredRavenMissile", -3, 0, 3)
		RVNS B 0 A_FireCustomMissile("PoweredRavenMissile", 6, 0, -6)
		RVNS B 0 A_FireCustomMissile("PoweredRavenMissile", -6, 0, 6)
		RVNS E 6 BRIGHT
		RVNS DC 3 BRIGHT
		RVNS B 6
		Goto Ready
	}
}

Actor RavenMissile
{
	Radius 11
	Height 8
	Scale 0.75
	Speed 45
	Damage (Random(60, 60))
	Projectile
	+THRUGHOST
	+BLOODLESSIMPACT
	SeeSound "Weapons/RavenStaff/Fire12"
	DeathSound "Weapons/RavenStaff/Hit"
	Obituary "%o was pecked down by %k's Raven Staff"
	States
	{
	Spawn:
		RVNP ABCD 2 BRIGHT A_SpawnItemEx("RavenTrail", -2, random(-2,2), random(-2,2))
		Loop

	Death:
		FIR3 A 0 Bright A_SetTranslucent(0.75, 1)
		TNT1 AAAA 0 A_SpawnItemEX("HEExplosionParticleSpawner")
		TNT1 A 0 A_CustomMissile("ExplosionSplashSpawner")
		FIR3 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	}
}

Actor RavenTrail
{
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOGRAVITY
	+SHADOW
	+NOTELEPORT
	+CANNOTPUSH
	+NODAMAGETHRUST
	+DONTSPLASH
	+NOGRAVITY
	RenderStyle Add
	Scale 0.5
	States
	{
	Spawn:
		PUF2 ABCDE 3 BRIGHT A_FadeOut(0.07)
		Stop
	}
}

Actor PoweredRavenMissile : RavenMissile
{
	Radius 11
	Height 8
	Speed 15
	Damage (random(2,2))
	Projectile
	+SeekerMissile
	+Ripper
	-BloodlessImpact
	+HexenBounce
	+CanBounceWater
	-ThruGhost
	BounceFactor 1.0
	RenderStyle Translucent
	SeeSound ""
	Obituary "%o was swarmed by %k's ravens"
	Alpha 0.75
	ReactionTime 10
	States
	{
	Spawn:
		RVSR A 0 A_PlaySound("Weapons/RavenStaff/RavenSight")
		RVSR ABCD 1 Bright
	Idle:
		RVSR A 4 Bright A_SeekerMissile(40, 60, SMF_LOOK | SMF_PRECISE)
		TNT1 A 0 Bright A_SpawnItemEx("RavenFeather", 0, 0, 0, 0, 0, 0, 0, 0, 192)
		RVSR B 4 Bright A_SeekerMissile(40, 60, SMF_LOOK | SMF_PRECISE)
		TNT1 A 0 Bright A_SpawnItemEx("RavenFeather", 0, 0, 0, 0, 0, 0, 0, 0, 192)
		RVSR C 4 Bright A_SeekerMissile(40, 60, SMF_LOOK | SMF_PRECISE)
		TNT1 A 0 Bright A_SpawnItemEx("RavenFeather", 0, 0, 0, 0, 0, 0, 0, 0, 192)
		RVSR D 4 Bright A_SeekerMissile(40, 60, SMF_LOOK | SMF_PRECISE)
		TNT1 A 0 Bright A_SpawnItemEx("RavenFeather", 0, 0, 0, 0, 0, 0, 0, 0, 192)
		TNT1 A 0 Bright A_Countdown
		Loop

	Death:
		TNT1 A 0 A_PlaySound("Weapons/RavenStaff/RavenDeath")
		TNT1 AAAA 0 A_SpawnItemEx("RavenFeather", 0, 0, 0, Random(2, 2), 0, Random(2, 2), Random(0, 360), 128)
		TNT1 A 0 A_SpawnItemEx("ExplosionSmoke", 0, 0, 0, Random(2, 2), 0, Random(2, 2), Random(0, 360), 128)
		TNT1 A 1 A_Stop
		Stop
	}
}

Actor RavenFeather
{
	Radius 2
	Height 4
	+MISSILE
	+DropOff
	+NoTeleport
	+CannotPush
	+WindThrust
	+DontSplash
	+RANDOMIZE
	Gravity 0.125
	States
	{
	Spawn:
		RVSR MNOPQPON 3
		Loop

	Death:
		RVSR O 6
		Stop
	}
}
Actor Corvus : PlayerPawn Replaces HereticPlayer
{
	Health 100
	Radius 16
	Height 56
	Mass 100
	Painchance 255
	Speed 1
	Species "Marines"
	Player.DisplayName "Corvus"
	Player.SoundClass "Corvus"
	Player.StartItem "HEGoldWand"
	Player.StartItem "HEStaff"
	Player.StartItem "HEGoldWandAmmo", 50
	Player.StartItem "IsPlayer", 1
	Player.StartItem "JustStartedGame", 1
	Player.StartItem "IsNOTTacticalClass", 1
	Player.StartItem "IsCorvus", 1

	Player.WeaponSlot 1, HEStaff, HEGauntlets
	Player.WeaponSlot 2, HEGoldWand
	Player.WeaponSlot 3, HECrossbow
	Player.WeaponSlot 4, HEBlaster
	Player.WeaponSlot 5, HESkullRod
	Player.WeaponSlot 6, HEPhoenixRod
	Player.WeaponSlot 7, RavenStaff

	Player.ColorRange 225, 240
	Player.Colorset 0, "Green",			225, 240,  238
	Player.Colorset 1, "Yellow",		114, 129,  127
	Player.Colorset 2, "Red",			145, 160,  158
	Player.Colorset 3, "Blue",			190, 205,  203
	// Doom Legacy additions
	Player.Colorset 4, "Brown",			 67,  82,   80
	Player.Colorset 5, "Light Gray",	  9,  24,   22
	Player.Colorset 6, "Light Brown",	 74,  89,   87
	Player.Colorset 7, "Light Red",		150, 165,  163
	Player.Colorset 8, "Light Blue",	192, 207,  205
	Player.Colorset 9, "Beige",			 95, 110,  108

	States
	{
	Spawn:
		CORV A 0
		NULL A 0 SetPlayerProperty(0,0,0)//unfreeze on level change/death when in a vehicle
		NULL A 0 SetPlayerProperty(0,0,4)//unfreeze on level change/death when on the bike
		TNT1 A 0 A_Changeflag("NOPAIN", 0)
		CORV A 0 A_TakeInventory("PlayerIsDead", 1)
		CORV A 0 A_TakeInventory("ChainguyguyContinue", 1)
		CORV A 0 A_TakeInventory("HasInfrared", 1)
		CORV A 0 ACS_NamedExecuteAlways("CheckVoodoo", 0, 0, 0, 0)//Check if a voodoo doll, if true goto VoodooDoll state
		PLAY A 0 ACS_NamedExecuteAlways("BDTacticalSpeed", 0, 0, 0, 0)//Makes player faster if tactical class.
		CORV A 0 ACS_NamedExecuteAlways("BDBlackMetal", 0, 0, 0, 0)//Check if playing in realism mode.
		CORV A 0 ACS_NamedExecuteAlways("BDStopCamera", 0, 0, 0, 0)//Disable Camera.
		CORV A 0 ACS_NamedExecuteAlways("CheckIfDM", 0, 0, 0, 0)//Check if Deathmatch
		CORV A 4
		Goto StandStill
	StandStill:
		CORV A 0
		TNT1 A 0 A_JumpIf (velx >=6 || velx <=-6, "See")
		TNT1 A 0 A_JumpIf (vely >=6 || vely <=-6, "See")
        CORV A 4 A_JumpIf (velz !=0, "IsJumpin")
		TNT1 A 0 A_JumpIf (velx >=6 || velx <=-6, "See")
		TNT1 A 0 A_JumpIf (vely >=6 || vely <=-6, "See")
        CORV A 4 A_JumpIf (velz !=0, "IsJumpin")
		Loop
	See:
		CORV AB 4
		CORV A 0 A_SpawnItemEx("FootStep6", 0, 0, 8, 0, 0, 0, 0, SXF_SETMASTER )
		CORV CD 4
		Goto StandStill
	IsJumpin:
        CORV A 0
		TNT1 A 0 A_GiveInventory("IsFloating", 1)
		CORV A 4 A_JumpIf (velz != 0, "InAir")
		Goto StandStill
	InAir:
		TNT1 A 0 A_GiveInventory("IsFloating", 1)
		CORV A 4 A_JumpIf (velz == 0, "Land")
		Loop
	Land:
        TNT1 A 0
		TNT1 A 0 A_TakeInventory("IsFloating", 100)
		CORV A 0 A_SpawnItemEx("FootStepStrong", 0, 0, 8, 0, 0)
		CORV A 4 A_JumpIf (velz != 0, "InAir")
		Goto StandStill
	Melee:
	Missile:
		CORV F 6 BRIGHT
		CORV E 12
		Goto Spawn
	Pain:
		CORV G 4
		CORV G 4 A_Pain
		Goto Spawn
	Death:
		CORV H 6 A_PlayerSkinCheck("AltSkinDeath")
		CORV I 6 A_PlayerScream
		CORV JK 6
		CORV L 6 A_NoBlocking
		CORV MNO 6
		CORV P -1
		Stop
	XDeath:
		CORV Q 0 A_PlayerSkinCheck("AltSkinXDeath")
		CORV Q 5 A_PlayerScream
		CORV R 0 A_NoBlocking
		CORV R 5 A_SkullPop
		CORV STUVWX 5
		CORV Y -1
		Stop
	Burn:
		FDTH A 5 BRIGHT A_PlaySound("*burndeath")
		FDTH B 4 BRIGHT
		FDTH C 5 BRIGHT
		FDTH D 4 BRIGHT A_PlayerScream
		FDTH E 5 BRIGHT
		FDTH F 4 BRIGHT
		FDTH G 5 BRIGHT A_PlaySound("*burndeath")
		FDTH H 4 BRIGHT
		FDTH I 5 BRIGHT
		FDTH J 4 BRIGHT
		FDTH K 5 BRIGHT
		FDTH L 4 BRIGHT
		FDTH M 5 BRIGHT
		FDTH N 4 BRIGHT
		FDTH O 5 BRIGHT A_NoBlocking
		FDTH P 4 BRIGHT
		FDTH Q 5 BRIGHT
		FDTH R 4 BRIGHT
		ACLO E 35 A_CheckPlayerDone
		Wait
	AltSkinDeath:
		CORV H 10
		CORV I 10 A_PlayerScream
		CORV J 10 A_NoBlocking
		CORV KLM 10
		CORV N -1
		Stop
	AltSkinXDeath:
		CORV O 5
		CORV P 5 A_XScream
		CORV Q 5 A_NoBlocking
		CORV RSTUV 5
		CORV W -1
		Stop
	}
}
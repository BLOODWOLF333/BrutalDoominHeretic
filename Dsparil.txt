ACTOR HESorcererFX1 Replaces SorcererFX1
{
	Game Heretic
	SpawnID 144
	Radius 10
	Height 10
	Speed 20
	FastSpeed 28
	Damage 10
	DamageType Fire
	Projectile
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	RenderStyle Add
	States
	{
	Spawn:
		BLHD CD 1 BRIGHT A_SpawnItem ("FireballExplosionFlames")
		Loop
	Death:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("ExplosiveFlames",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_CustomMissile("HEExplosionparticles3", 0, 0, random (0, 360), 2, random (40, 90))
		TNT1 A 0 A_CustomMissile("HEExplosionparticles4", 0, 0, random (0, 360), 2, random (40, 90))
		TNT1 A 0 A_CustomMissile("BigBlackSmokeLarger", 0, 0, random (0, 360), 2, random (20, 90))
		TNT1 A 0 Radius_Quake(3, 8, 0, 15, 0)
		TNT1 A 0 A_SpawnItemEx("TinyBurningPiece", 0,0,5,random (-4, 4), random (-4, 4), random (1, 5))
		TNT1 A 0 A_SpawnItemEx("TinyBurningPiece2", 0,0,10,random (-4, 4), random (-4, 4), random (1, 5))
		TNT1 A 0 A_SpawnItemEx("TinyBurningPiece3", 0,0,15,random (-4, 4), random (-4, 4), random (1, 5))
		Stop
	}
}

ACTOR HESorcerer2 : Sorcerer2 Replaces Sorcerer2
{
	+NOBLOOD
	States
	{
	Spawn:
		SOR2 MN 10 A_Look
		Loop
	See:
		SOR2 MNOP 4 A_Chase
		Loop
	Rise:
		SOR2 AB 4
		SOR2 C 4 A_PlaySound("dsparil/rise", CHAN_BODY, 1, false, ATTN_NONE)
		SOR2 DEF 4
		SOR2 G 12 A_PlaySound("dsparil/sight", CHAN_BODY, 1, false, ATTN_NONE)
		Goto See
	Pain:
		SOR2 Q 3
		SOR2 Q 6 A_Pain
		Goto See
	Missile:
		SOR2 R 9 A_Srcr2Decide
		SOR2 S 9 A_FaceTarget
		SOR2 T 20 A_Srcr2Attack
		Goto See
	Teleport:
		SOR2 LKJIHG 6
		Goto See
	Death:
		SDTH A 8 A_Sor2DthInit
		SDTH B 8
		SDTH C 8 A_PlaySound("dsparil/scream", CHAN_BODY, 1, false, ATTN_NONE)
	DeathLoop:
		SDTH DE 7
		TNT1 AAAA 0 A_SpawnItemEx("BlueFlames",random(-16,16),random(-16,16),random(65,75),0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAA 0 A_SpawnItemEx("BlueParticleSpawner",random(-16,16),random(-16,16),random(65,75))
		TNT1 A 0 A_CustomMissile("BigBlackSmokeLarger", random(65,75), 0, random (0, 360), 2, random (20, 90))
		SDTH F 7 A_Sor2DthLoop
		TNT1 AAAA 0 A_SpawnItemEx("BlueFlames",random(-16,16),random(-16,16),random(65,75),0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAA 0 A_SpawnItemEx("BlueParticleSpawner",random(-16,16),random(-16,16),random(65,75))
		TNT1 A 0 A_CustomMissile("BigBlackSmokeLarger", random(65,75), 0, random (0, 360), 2, random (20, 90))
		TNT1 A 0 Radius_Quake(3, 8, 0, 15, 0)
		SDTH H 6
		TNT1 AAAA 0 A_SpawnItemEx("BlueFlames",random(-16,16),random(-16,16),random(65,75),0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 AAAA 0 A_SpawnItemEx("BlueParticleSpawner",random(-16,16),random(-16,16),random(65,75))
		TNT1 A 0 A_CustomMissile("BigBlackSmokeLarger", random(65,75), 0, random (0, 360), 2, random (20, 90))
		SDTH I 18
		SDTH J 6 A_NoBlocking
		SDTH K 6 A_PlaySound("dsparil/bones", CHAN_BODY, 1, false, ATTN_NONE)
		SDTH LMN 6
		SDTH O -1 A_BossDeath
		Stop
	}
}

ACTOR HESorcerer2FX1 : Sorcerer2FX1 Replaces Sorcerer2FX1
{
	Scale 0.375
	States
	{
	Spawn:
		SPKB A 3 BRIGHT A_BlueSpark
		Loop
	Death:
		FX16 G 5 BRIGHT A_Explode(random[S2FX1](80,111))
		TNT1 A 0 A_CustomMissile ("BlueFire", 0, 0, random (0, 360), CMF_AIMDIRECTION | CMF_ABSOLUTEPITCH | CMF_ABSOLUTEANGLE, random(60,90))
		TNT1 A 0 A_SpawnItemEx("BlueParticleSpawner")
		TNT1 A 0 A_CustomMissile ("ExplosionSmoke", 1, 0, random (0, 360), 2, random (0, 160))
		FX16 HIJKL 5 BRIGHT
		Stop
	}
}